buildscript {
    ext.kotlin_version = '1.2.31'
    ext.vok_version = '0.4.5'
    ext.karibudsl_version = '0.4.0'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.31"
    // need to use Gretty here because of https://github.com/johndevs/gradle-vaadin-plugin/issues/317
    id "org.akhikhl.gretty" version "2.0.0"
    id 'com.devsoap.plugin.vaadin' version '1.3.1'
}

defaultTasks 'clean', 'build'


allprojects {
    group 'com.example.vok'
    version '1.0-SNAPSHOT'
}

subprojects {

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'http://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        maven { url "https://dl.bintray.com/mvysny/vaadin-on-kotlin" }
        maven { url "https://dl.bintray.com/mvysny/github" }
    }

    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    // for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
